james_wu

high

# Sense redeem is vulnerable to reentrancy

minhtrng

high

# Sense redeem is vulnerable to reentrancy

## Summary

Insufficient reentrancy protection in `Redeemer` allows for inflating the `holdings` variable via reentrancy and subsequently withdrawing more than should be allowed.

## Vulnerability Detail

The `Redeemer.redeem` function related to Sense
allows passing in an arbitrary address that will be called. This enables reentrancy by passing in a malicious contract that reenters the `redeem` function, which causes the `holdings` variable to inflate for a market. 

Calling the `redeem` function that burns illuminate tokens and sends underlying to the sender, will then cause more tokens to be sent than the sender was eligible for.

## Impact

This will leave the protocol with a deficit in underlying tokens. This can go as far as the exploiter redeeming all matured principal tokens (as the `redeem` functions are accessible to the public) and then taking all the underlying tokens that have been sent to the `Redeemer`.

## Code Snippet
https://github.com/sherlock-audit/2023-01-illuminate/blob/main/src/Redeemer.sol#L426-L504
```solidity
/// @notice redeem method signature for Sense
/// @param p principal value according to the MarketPlace's Principals Enum
/// @param u address of an underlying asset
/// @param m maturity (timestamp) of the market
/// @param s Sense's maturity is needed to extract the pt address
/// @param a Sense's adapter index
/// @param periphery Sense's periphery contract, used to get the verified adapter
/// @return bool true if the redemption was successful
function redeem(
    uint8 p,
    address u,
    uint256 m,
    uint256 s,
    uint256 a,
    address periphery
) external unpaused(u, m) returns (bool) {
    // Get Sense's principal token for this market
    IERC20 token = IERC20(
        IMarketPlace(marketPlace).markets(
            u,
            m,
            uint8(MarketPlace.Principals.Sense)
        )
    );


    // Confirm the periphery is verified by the lender
    if (IERC20(u).allowance(lender, periphery) == 0) {
        revert Exception(29, 0, 0, address(0), address(0));
    }


    // Cache the lender to save on SLOAD operations
    address cachedLender = lender;


    // Get the balance of tokens to be redeemed by the user
    uint256 amount = token.balanceOf(cachedLender);


    // Transfer the user's tokens to the redeem contract
    Safe.transferFrom(token, cachedLender, address(this), amount);


    // Calculate the balance of the redeemer contract
    uint256 redeemable = token.balanceOf(address(this));


    // Get the starting balance to verify the amount received afterwards
    uint256 starting = IERC20(u).balanceOf(address(this));


    // Get the existing balance of Sense PTs
    uint256 senseBalance = token.balanceOf(address(this));


    // Get the divider from the periphery
    ISenseDivider divider = ISenseDivider(
        ISensePeriphery(periphery).divider()
    );


    // Get the adapter from the divider
    address adapter = divider.adapterAddresses(a);


    // Redeem the tokens from the Sense contract
    ISenseDivider(divider).redeem(adapter, s, senseBalance);


    // Get the compounding token that is redeemed by Sense
    address compounding = ISenseAdapter(adapter).target();


    // Redeem the compounding token back to the underlying
    IConverter(converter).convert(
        compounding,
        u,
        IERC20(compounding).balanceOf(address(this))
    );


    // Get the amount received
    uint256 redeemed = IERC20(u).balanceOf(address(this)) - starting;


    // Update the holdings for this market
    holdings[u][m] = holdings[u][m] + redeemed;


    emit Redeem(p, u, m, redeemed, redeemable, msg.sender);
    return true;
}
```

The redeem function for sense requires passing a Sense Periphery:

```solidity
/// @param periphery Sense's periphery contract, used to get the verified adapter
```

This is used in the function like this:

```solidity
// Get the divider from the periphery
ISenseDivider divider = ISenseDivider(
    ISensePeriphery(periphery).divider();
);

// Get the adapter from the divider
address adapter = divider.adapterAddresses(a);

// Redeem the tokens from the Sense contract
ISenseDivider(divider).redeem(adapter, s, senseBalance);
```

The `divider.redeem` function is where the control flow can be taken over and a reentrancy could be initiated.

The holdings are updated at the end of the function:

```solidity
// Get the amount received
uint256 redeemed = IERC20(u).balanceOf(address(this)) - starting;

// Update the holdings for this market
holdings[u][m] = holdings[u][m] + redeemed;
```

To exploit this, only the last reentrancy call would need to send the underlying tokens to the contract, the holdings will increase for each reentrancy call by the same amount, as the difference between starting- and balanceOf will be the same for all.

Note: 
- an expliting contract(fake periphery) should have position in platform to pass below check.
```solidity
// Confirm the periphery is verified by the lender
if (IERC20(u).allowance(lender, periphery) == 0) {
    revert Exception(29, 0, 0, address(0), address(0));
}
```
- an exploiting contract(fake Adapter), would also need to make sure that the call to `converter.convert` does not fail by mocking the target() function inside accordingly to return correct compounding token, but that raises the complexity only slightly and does not impact the exploit path.

## Tool used

Manual Review

## Recommendation

Add a reentrancy protection modifier such as OpenZeppelins `nonreentrant`. Also consider validating or whitelisting the passed contract addresses.