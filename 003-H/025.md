cccz

high

# Incorrect parameters when calling the initiate function, allowing the user to uses the balance of the Lender contract to pay SwivelFee.

## Summary
Incorrect parameters when calling the initiate function, allowing the user to uses the balance of the Lender contract to pay SwivelFee.
## Vulnerability Detail
In Swivel's Lend(), the fees are first charged as IlluminateFee.
```solidity
        uint256 fee = lent / feenominator;

        {
            // Get last order to be processed's index
            uint256 lastIndex = a.length - 1;

            // Add the accumulated fees to the total
            a[lastIndex] = a[lastIndex] - fee; // Revert here if fee not paid

            // Extract fee
            fees[u] += fee;
        }
```
This part of the fee is stored in the fees variable and is withdrawn by admin calling withdrawFee
```solidity
    function withdrawFee(address e) external authorized(admin) returns (bool) {
        // Get the token to be withdrawn
        IERC20 token = IERC20(e);

        // Get the balance to be transferred
        uint256 balance = fees[e];

        // Reset accumulated fees of the token to 0
        fees[e] = 0;

        // Transfer the accumulated fees to the admin
        Safe.transfer(token, admin, balance);

        return true;
    }
```
When the ISwivel.Initiate function is called, the fees are second charged as SwivelFee. 
But SwivelFee is not charged from the parameter `a` of the initiate function, but is paid by msg.sender, in this case the Lender contract.( see @audit )
```solidity
    function initiateZcTokenFillingVaultInitiate(
        Hash.Order calldata o,
        uint256 a,
        Sig.Components calldata c
    ) internal {
        bytes32 hash = validOrderHash(o, c);
        uint256 amount = a + filled[hash];

        if (amount > o.principal) {
            revert Exception(5, amount, o.principal, address(0), address(0));
        }

        filled[hash] = amount;

        IERC20 uToken = IERC20(o.underlying);

        uint256 premiumFilled = (a * o.premium) / o.principal;
        Safe.transferFrom(uToken, o.maker, msg.sender, premiumFilled);

        // transfer principal + fee in underlying to swivel (from sender)
        uint256 fee = premiumFilled / feenominators[0];
        Safe.transferFrom(uToken, msg.sender, address(this), (a + fee)); //@audit: The additional fees are charged from msg.sender

```
Consider the following cases, where IlluminateFee is 1% and SwivelFee is 2%
User A provides 100 uTokens calls Swivel's Lend(), a[0] = 100
In the following calculation, fee == 1, a[0] == 99 for the initiate function.
In the initiate function, SwivelFee = 99* 2% = 1.98, but instead of deducting this fee from 99, 99+1.98 = 100.98 tokens will be sent from the Lender contract to the Swivel contract.
```solidity
       uint256 fee = lent / feenominator;

        {
            // Get last order to be processed's index
            uint256 lastIndex = a.length - 1;

            // Add the accumulated fees to the total
            a[lastIndex] = a[lastIndex] - fee; // Revert here if fee not paid

            // Extract fee
            fees[u] += fee;
        }

        uint256 received;
        {
            // Get the starting amount of principal tokens
            uint256 startingZcTokens = IERC20(
                IMarketPlace(marketPlace).markets(u, m, p)
            ).balanceOf(address(this));

            // Fill the given orders on Swivel
            ISwivel(swivelAddr).initiate(o, a, s);
```
The user uses the balance of the Lender contract to pay SwivelFee.
And, in Swivel's Lend(), fees[u] increases by 1, but the contract balance decreases by 1.98-1=0.98, which makes fees[u] larger than the contract balance, withdrawFee will fail due to insufficient contract balance
## Impact
Users can use the Lender contract balance to pay Swivel's fee, and the withdrawFee function will fail

## Code Snippet
https://github.com/sherlock-audit/2023-01-illuminate/blob/main/src/Lender.sol#L498-L519
https://github.com/sherlock-audit/2023-01-illuminate/blob/main/src/Lender.sol#L999-L1016
https://github.com/Swivel-Finance/swivel/blob/main/contracts/v3/src/Swivel.sol#L229-L250
## Tool used

Manual Review

## Recommendation
In Swivel's Lend(), use o.premium/o.principal/feenominators[0] to calculate the SwivelFee and deduct it from `a`.
```solidity
        uint256 premiumFilled = (a * o.premium) / o.principal;
        uint256 fee = premiumFilled / feenominators[0];
```